Value Filldown VRF (\S+)
Value Filldown PROTOCOL (\w)
Value Filldown TYPE (\S{0,2})
Value Required,Filldown NETWORK (\d{1,3}.\d{1,3}.\d{1,3}.\d{1,3})
Value Filldown MASK (\d{1,2})
Value DISTANCE (\d+)
Value METRIC (\d+)
Value NEXTHOP_IP (\d{1,3}.\d{1,3}.\d{1,3}.\d{1,3})
Value NEXTHOP_IF ([\d\w\-\.:/]+)
Value UPTIME (\d[\w:\.]+)

Start
  ^Routing\s+table\s+for\s+VRF=${VRF}\s*$$ -> Record
  # Match the Network/Prefix Line
  # Match directly connected route with explicit mask
  ^${PROTOCOL}(\s|\*)${TYPE}\s+${NETWORK}\/${MASK}\sis\sdirectly\sconnected,\s+${NEXTHOP_IF} -> Record
  # Match directly connected route with metric and distance
  ^${PROTOCOL}(\s|\*)${TYPE}\s+${NETWORK}\/${MASK}\s+\[${DISTANCE}/${METRIC}\]\sis\sdirectly\sconnected,\s+${NEXTHOP_IF} -> Record
  # Match regular routes, with mask, where all data in same line
  ^${PROTOCOL}(\s|\*)${TYPE}\s+${NETWORK}\/${MASK}\s\[${DISTANCE}/${METRIC}\]\svia\s${NEXTHOP_IP}(,\s${NEXTHOP_IF})? -> Record
  # Match load-balanced routes
  ^\s+\[${DISTANCE}/${METRIC}\]\svia\s${NEXTHOP_IP}(,\s${NEXTHOP_IF})? -> Record
  # Match "is a summary" routes (often Null0)
  ^${PROTOCOL}(\s|\*)${TYPE}\s+${NETWORK}\/${MASK}\s+\[${DISTANCE}/${METRIC}\]\sis\sa\ssummary,\s${NEXTHOP_IF} -> Record

EOF
